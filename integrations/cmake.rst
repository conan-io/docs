.. _cmake:

|cmake_logo| CMake
__________________



If you are using *CMake* to build your project, you can use the *cmake* generator to manage all your requirements.


**conanfile.txt**

.. code-block:: text

   ...

   [generators]
   cmake


When **conan install** is executed, a file named ``conanbuildinfo.cmake`` is created.

We can include ``conanbuildinfo.cmake`` in our project's ``CMakeLists.txt`` to manage our requirements.


This is the ``CMakeLists.txt`` file we used in the :ref:`Getting started<getting_started>` example:

.. code-block:: cmake

   project(FoundationTimer)
   cmake_minimum_required(VERSION 2.8.12)

   include(${CMAKE_BINARY_DIR}/conanbuildinfo.cmake)
   conan_basic_setup()

   add_executable(timer timer.cpp)
   target_link_libraries(timer ${CONAN_LIBS})


- **include(conanbuildinfo.cmake)** will include the file generated by our **cmake** [generator]
- **conan_basic_setup()** call will asign to **CMake** all the needed variables for linking with our requirements.
- **${CONAN_LIBS}** contains the libraries to link with. So ``target_link_libraries()``, works just fine.


Let's take a look at the generated ``conanbuildinfo.cmake`` file:


.. code-block:: cmake

    set(CONAN_POCO_ROOT "/home/laso/.conan/data/Poco/1.6.1/lasote/stable/package/afafc631e705f7296bec38318b28e4361ab6787c")
    set(CONAN_INCLUDE_DIRS_POCO "/home/laso/.conan/data/Poco/1.6.1/lasote/stable/package/afafc631e705f7296bec38318b28e4361ab6787c/include")
    set(CONAN_LIB_DIRS_POCO "/home/laso/.conan/data/Poco/1.6.1/lasote/stable/package/afafc631e705f7296bec38318b28e4361ab6787c/lib")
    set(CONAN_BIN_DIRS_POCO "/home/laso/.conan/data/Poco/1.6.1/lasote/stable/package/afafc631e705f7296bec38318b28e4361ab6787c/bin")
    set(CONAN_LIBS_POCO PocoUtil PocoXML PocoJSON PocoMongoDB PocoNet PocoCrypto PocoData PocoDataSQLite PocoZip PocoFoundation pthread dl rt)
    set(CONAN_DEFINES_POCO -Dshared=OFF -DPOCO_NO_AUTOMATIC_LIBS)
    set(CONAN_COMPILE_DEFINITIONS_POCO shared=OFF POCO_NO_AUTOMATIC_LIBS)
    set(CONAN_CXX_FLAGS_POCO "")
    set(CONAN_SHARED_LINK_FLAGS_POCO "")
    set(CONAN_EXE_LINKER_FLAGS_POCO "")
    set(CONAN_C_FLAGS_POCO "")

    set(CONAN_ZLIB_ROOT "/home/laso/.conan/data/zlib/1.2.8/lasote/stable/package/3b92a20cb586af0d984797002d12b7120d38e95e")
    set(CONAN_INCLUDE_DIRS_ZLIB "/home/laso/.conan/data/zlib/1.2.8/lasote/stable/package/3b92a20cb586af0d984797002d12b7120d38e95e/include")
    set(CONAN_LIB_DIRS_ZLIB "/home/laso/.conan/data/zlib/1.2.8/lasote/stable/package/3b92a20cb586af0d984797002d12b7120d38e95e/lib")
    set(CONAN_BIN_DIRS_ZLIB "/home/laso/.conan/data/zlib/1.2.8/lasote/stable/package/3b92a20cb586af0d984797002d12b7120d38e95e/bin")
    set(CONAN_LIBS_ZLIB z)
    set(CONAN_DEFINES_ZLIB )
    set(CONAN_COMPILE_DEFINITIONS_ZLIB )
    set(CONAN_CXX_FLAGS_ZLIB "")
    set(CONAN_SHARED_LINK_FLAGS_ZLIB "")
    set(CONAN_EXE_LINKER_FLAGS_ZLIB "")
    set(CONAN_C_FLAGS_ZLIB "")

    set(CONAN_OPENSSL_ROOT "/home/laso/.conan/data/OpenSSL/1.0.2d/lasote/stable/package/dd8a0e4171607d74dee9fd0c51153a922d849535")
    set(CONAN_INCLUDE_DIRS_OPENSSL "/home/laso/.conan/data/OpenSSL/1.0.2d/lasote/stable/package/dd8a0e4171607d74dee9fd0c51153a922d849535/include")
    set(CONAN_LIB_DIRS_OPENSSL "/home/laso/.conan/data/OpenSSL/1.0.2d/lasote/stable/package/dd8a0e4171607d74dee9fd0c51153a922d849535/lib")
    set(CONAN_BIN_DIRS_OPENSSL "/home/laso/.conan/data/OpenSSL/1.0.2d/lasote/stable/package/dd8a0e4171607d74dee9fd0c51153a922d849535/bin")
    set(CONAN_LIBS_OPENSSL ssl crypto dl)
    set(CONAN_DEFINES_OPENSSL )
    set(CONAN_COMPILE_DEFINITIONS_OPENSSL )
    set(CONAN_CXX_FLAGS_OPENSSL "")
    set(CONAN_SHARED_LINK_FLAGS_OPENSSL "")
    set(CONAN_EXE_LINKER_FLAGS_OPENSSL "")
    set(CONAN_C_FLAGS_OPENSSL "")

    set(CONAN_PACKAGE_NAME Poco)
    set(CONAN_PACKAGE_VERSION 1.7.3)
    set(CONAN_DEPENDENCIES OpenSSL zlib)
    set(CONAN_INCLUDE_DIRS "/home/laso/.conan/data/Poco/1.6.1/lasote/stable/package/afafc631e705f7296bec38318b28e4361ab6787c/include"
                "/home/laso/.conan/data/OpenSSL/1.0.2d/lasote/stable/package/dd8a0e4171607d74dee9fd0c51153a922d849535/include"
                "/home/laso/.conan/data/zlib/1.2.8/lasote/stable/package/3b92a20cb586af0d984797002d12b7120d38e95e/include" ${CONAN_INCLUDE_DIRS})
    set(CONAN_LIB_DIRS "/home/laso/.conan/data/Poco/1.6.1/lasote/stable/package/afafc631e705f7296bec38318b28e4361ab6787c/lib"
                "/home/laso/.conan/data/OpenSSL/1.0.2d/lasote/stable/package/dd8a0e4171607d74dee9fd0c51153a922d849535/lib"
                "/home/laso/.conan/data/zlib/1.2.8/lasote/stable/package/3b92a20cb586af0d984797002d12b7120d38e95e/lib" ${CONAN_LIB_DIRS})
    set(CONAN_BIN_DIRS "/home/laso/.conan/data/Poco/1.6.1/lasote/stable/package/afafc631e705f7296bec38318b28e4361ab6787c/bin"
                "/home/laso/.conan/data/OpenSSL/1.0.2d/lasote/stable/package/dd8a0e4171607d74dee9fd0c51153a922d849535/bin"
                "/home/laso/.conan/data/zlib/1.2.8/lasote/stable/package/3b92a20cb586af0d984797002d12b7120d38e95e/bin" ${CONAN_BIN_DIRS})
    set(CONAN_LIBS PocoUtil PocoXML PocoJSON PocoMongoDB PocoNet PocoCrypto PocoData PocoDataSQLite PocoZip PocoFoundation pthread dl rt ssl crypto z ${CONAN_LIBS})
    set(CONAN_DEFINES -Dshared=OFF -DPOCO_NO_AUTOMATIC_LIBS ${CONAN_DEFINES})
    set(CONAN_CXX_FLAGS " ${CONAN_CXX_FLAGS}")
    set(CONAN_SHARED_LINK_FLAGS " ${CONAN_SHARED_LINK_FLAGS}")
    set(CONAN_EXE_LINKER_FLAGS " ${CONAN_EXE_LINKER_FLAGS}")
    set(CONAN_C_FLAGS " ${CONAN_C_FLAGS}")
    set(CONAN_CMAKE_MODULE_PATH "/home/laso/.conan/data/Poco/1.6.1/lasote/stable/package/afafc631e705f7296bec38318b28e4361ab6787c" "/home/laso/.conan/data/zlib/1.2.8/lasote/stable/package/3b92a20cb586af0d984797002d12b7120d38e95e" "/home/laso/.conan/data/OpenSSL/1.0.2d/lasote/stable/package/dd8a0e4171607d74dee9fd0c51153a922d849535" ${CONAN_CMAKE_MODULE_PATH})

    macro(conan_basic_setup)
        conan_check_compiler()
        conan_output_dirs_setup()
        conan_set_find_library_paths()
        if(NOT "${ARGV0}" STREQUAL "TARGETS")
            message(STATUS "Conan: Using cmake global configuration")
            conan_global_flags()
        else()
            message(STATUS "Conan: Using cmake targets configuration")
            conan_define_targets()
        endif()
        conan_set_rpath()
        conan_set_vs_runtime()
        conan_set_libcxx()
        conan_set_find_paths()
    endmacro()

   # ... macros code...


As we can see, conan is preparing some variables:

* ``CONAN_INCLUDE_DIRS``: The headers folders from the requirements.
* ``CONAN_LIB_DIRS``: The library folders from the requirements.
* ``CONAN_BIN_DIRS``: The binary folders from the requirements.
* ``CONAN_LIBS``: The name of the libs we have to link with.
* ``CONAN_DEFINES``: Defines, observe that two are defined, "shared" and ``POCO_NO_AUTOMATIC_LIBS``, that correspond to options.
* ``CONAN_COMPILE_DEFINITIONS``: Defines, equal to ``CONAN_DEFINES`` but without the ``-D``, as required by cmake targets
* ``CONAN_C_FLAGS``: Flags for C. Not specified for Poco nor its requirements.
* ``CONAN_CXX_FLAGS``: Flags for CXX. Not specified for Poco nor its requirements.
* ``CONAN_SHARED_LINK_FLAGS``: Shared flags for CXX. Not specified for Poco nor its requirements.
* ``CONAN_EXE_LINKER_FLAGS``: Exe linker flags for CXX. Not specified for Poco nor its requirements.


Conan also provides the same variables isolated for each requirement, so you can handle the requirements individually:
``CONAN_POCO_ROOT, CONAN_INCLUDE_DIRS_POCO,CONAN_INCLUDE_DIRS_OPENSSL``,  etc

For the root package, the name, version and dependencies are also defined in ``CONAN_PACKAGE_NAME, CONAN_PACKAGE_VERSION, CONAN_DEPENDENCIES``


Consuming ``conanbuildinfo.cmake``
==================================

The inclusion of ``conanbuildinfo.cmake`` doesn't alter cmake environment at all, it just provide ``CONAN_`` variables and some useful macros.

The simplest way to consume it would be to invoke the ``conan_basic_setup()`` macro, which will basically
set global include directories, libraries directories, definitions, etc. so typically is enough to do:

.. code-block:: cmake

    include(${CMAKE_BINARY_DIR}/conanbuildinfo.cmake)
    conan_basic_setup()

    add_executable(timer timer.cpp)
    target_link_libraries(timer ${CONAN_LIBS})

The ``conan_basic_setup()`` is split in smaller macros, that should be self explanatory. If you need to do
something different, you can just use them individually.

The above example is using the macro ``conan_global_flags()``
which sets the global environment. For **modern cmake (>=3.1.2)**, the macro ``conan_define_targets()``
defines cmake ``INTERFACE IMPORTED`` targets, one per package, so they can be used to link with, instead
of using global cmake setup. The name of the targets is ``CONAN_PKG::PackageName``

.. code-block:: cmake

    include(${CMAKE_BINARY_DIR}/conanbuildinfo.cmake)
    conan_basic_setup(TARGETS)

    add_executable(timer timer.cpp)
    target_link_libraries(timer CONAN_PKG::Poco)

CMake multi-configuration environments
=======================================

There are environments, like Visual Studio and XCode IDEs that do not configure for a specific
``build_type``, like Debug or Release, but rather can be used for both and switch among Debug and
Release configurations with a combo box or similar control. The project configuration for cmake
is different, in multi-configuration environments, the flow would be:

.. code-block:: bash

    $ cmake .. -G "Visual Studio 14 Win64"
    # Now open the IDE (.sln file) or
    $ cmake --build . --config Release
    
While in single-configuration environments (Unix Makefiles, etc):

.. code-block:: bash

    $ cmake .. -G "Unix Makefiles" -DCMAKE_BUILD_TYPE=Release
    # Build from your IDE, launching make, or
    $ cmake --build .
    
The ``CMAKE_BUILD_TYPE`` default, if not specified is ``Debug``.

With the regular conan ``cmake`` generator, only 1 configuration at a time can be managed. Then,
it is a universal, homogeneous solution for all environments.
This is the recommended way, using the regular ``cmake`` generator, and just go to the command line and
switch among configurations:

.. code-block:: bash

    $ conan install -s build_type=Release -s compiler.runtime=MD ...
    # Work in release, then, to switch to Debug dependencies
    $ conan install -s build_type=Debug -s compiler.runtime=MDd ...


However, end consumers with heavy usage of the IDE, might want a multi-configuration build. The
``cmake_multi`` **experimental** generator is able to do that. First, both Debug and Release
dependencies have to be installed:

.. code-block:: bash

    $ conan install -g cmake_multi -s build_type=Release -s compiler.runtime=MD ... 
    $ conan install -g cmake_multi -s build_type=Debug -s compiler.runtime=MDd ...
    
These commands will generate 3 files: ``conanbuildinfo_release.cmake``, ``conanbuildinfo_debug.cmake``,
and ``conanbuildinfo_multi.cmake``, which includes the other two, and enables its use.

The consumer project might write a ``CMakeLists.txt`` like:

.. code-block:: cmake

    project(MyHello)
    cmake_minimum_required(VERSION 2.8.12)
    
    include(${CMAKE_BINARY_DIR}/conanbuildinfo_multi.cmake)
    conan_basic_setup()
    
    add_executable(say_hello main.cpp)
    foreach(_LIB ${CONAN_LIBS_RELEASE})
        target_link_libraries(say_hello optimized ${_LIB})
    endforeach()
    foreach(_LIB ${CONAN_LIBS_DEBUG})
        target_link_libraries(say_hello debug ${_LIB})
    endforeach()
    
Or, if using the modern cmake syntax with targets:

.. code-block:: cmake

    project(MyHello)
    cmake_minimum_required(VERSION 2.8.12)
    
    include(${CMAKE_BINARY_DIR}/conanbuildinfo_multi.cmake)
    conan_basic_setup(TARGETS)
    
    add_executable(say_hello main.cpp)
    target_link_libraries(say_hello CONAN_PKG::Hello1)

There's also a convenient macro for linking to all libraries:

.. code-block:: cmake

    project(MyHello)
    cmake_minimum_required(VERSION 2.8.12)

    include(${CMAKE_BINARY_DIR}/conanbuildinfo_multi.cmake)
    conan_basic_setup()

    add_executable(say_hello main.cpp)
    conan_target_link_libraries(say_hello)
    
    
With this approach, the end user can open the generated IDE project and switch among both
configurations, building the project, or from the command line:

.. code-block:: bash

    $ cmake --build . --config Release
    # And without having to conan install again, or do anything else
    $ cmake --build . --config Debug

Build automation
================

You can invoke CMake from your conanfile.py file and automate the build of your library/project.
Conan provides a `CMake()` helper. This helper is useful to call ``cmake`` command both for creating conan packages
or automating your project build with the ``conan build`` command. The ``CMake()`` helper will take into account
your settings to automatically set definitions and a generator according to your compiler, build_type, etc.
Check the section :ref:`Building with CMake<conanfile_py_managed_settings>`.


Find Packages
=============

If installed CMake already provides a FindXXX.cmake file for the library you are packaging, it should work automatically.

Variables **CMAKE_INCLUDE_PATH** and **CMAKE_LIBRARY_PATH** are set with the right requirements paths. CMake **find_library** function will be able to locate the libraries in the package's folders.

So, you can use **find_package** normally:


.. code-block:: cmake

    project(MyHello)
    cmake_minimum_required(VERSION 2.8.12)

    include(conanbuildinfo.cmake)
    conan_basic_setup()

    find_package("ZLIB")

    if(ZLIB_FOUND)
        add_executable(enough enough.c)
        include_directories(${ZLIB_INCLUDE_DIRS})
        target_link_libraries(enough ${ZLIB_LIBRARIES})
    else()
        message(FATAL_ERROR "Zlib not found")
    endif()


In addition to automatic **find_package** support, **CMAKE_MODULE_PATH** variable is set with your requirements root package paths. You can override the default behavior of any find_package() by creating a ``findXXX.cmake`` file in your package.

Creating a custom FindXXX.cmake file
------------------------------------

Sometimes the "official" CMake FindXXX.cmake scripts are not ready to find our libraries (not supported library names for specific settings, fixed installation directories like C:\\OpenSSL... etc)
Or maybe there is no "official" CMake script for our library.

So in these cases we can provide a custom **FindXXX.cmake** file in our conan packages:

1. Create a file named FindXXX.cmake and save it in your conan package root folder. Where XXX is the name of the library that we will use in the **find_package** CMake function.
For example, we create a ``FindZLIB.cmake`` and use ``find_package(ZLIB)``.
We recommend (if exists) to copy the original FindXXX.cmake file from kitware if provided (folder Modules/FindXXX.cmake) and modify it to help finding our library files, but it depends a lot, maybe you are interested in create a new one.

If it's not provided you can create a basic one, take a look to this example with the ZLIB library:

**FindZLIB.cmake**

.. code-block:: cmake

   find_path(ZLIB_INCLUDE_DIR NAMES zlib.h PATHS ${CONAN_INCLUDE_DIRS_ZLIB})
   find_library(ZLIB_LIBRARY NAMES ${CONAN_LIBS_ZLIB} PATHS ${CONAN_LIB_DIRS_ZLIB})

   set(ZLIB_FOUND TRUE)
   set(ZLIB_INCLUDE_DIRS ${ZLIB_INCLUDE_DIR})
   set(ZLIB_LIBRARIES ${ZLIB_LIBRARY})
   mark_as_advanced(ZLIB_LIBRARY ZLIB_INCLUDE_DIR)


In the first line we are finding the path where our headers should be found, we suggest the CONAN_INCLUDE_DIRS_XXX.
Then the same for the library names with CONAN_LIBS_XXX and the paths where the libs are CONAN_LIB_DIRS_XXX.

2. In your conanfile.py file add the ``FindXXX.cmake`` to the ``exports_sources`` field:


.. code-block:: python

   class HelloConan(ConanFile):
       name = "Hello"
       version = "0.1"
       ...
       exports_sources = ["FindXXX.cmake"]

3. In the package method, copy the ``FindXXX.cmake`` file to the root:



.. code-block:: python

   class HelloConan(ConanFile):
       name = "Hello"
       version = "0.1"
       ...
       exports_sources = ["FindXXX.cmake"]


       def package(self):
           ...
           self.copy("FindXXX.cmake", ".", ".")



.. |cmake_logo| image:: ../images/cmake_logo.png

.. _`conan's boost package`: https://github.com/lasote/conan-boost.git
.. _`conan's zlib package`: https://github.com/lasote/conan-zlib.git
