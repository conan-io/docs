#!groovyâ€‹

node('Linux') {
    String masterBranch = 'master'
    boolean isMaster = env.BRANCH_NAME == masterBranch

    stage('Clone sources') {
        checkout scm
    }

    def image = null
    stage('Build docker image') {
        image = docker.build('conan-docs', '-f .ci/Dockerfile .')  // It should cache the image
    }

    stage('Test build') {
        parallel html: {
            image.inside {
                sh 'make html'
            }
        },
        pdf: {
            image.inside {
                sh 'make latex'
            }
        },
        spelling: {
            image.inside {
                sh 'make spelling'
            }
        },
        linkcheck: {
            image.inside {
                def status = 0
                catchError(buildResult: 'SUCCESS', stageResult: 'UNSTABLE', message: 'Linkcheck failed! Please check links and fix broken ones.') {
                    status = sh(returnStatus: true, script: 'make linkcheck > links_output.txt')
                }
                if (status != 0) {
                    def output = sh(returnStdout: true , script: 'cat links_output.txt | grep broken')
                    println output
                    def subject = "Broken Links in Docs: Job '${env.JOB_NAME} [${env.BUILD_NUMBER}]'"
                    def summary = "${subject} (${env.BUILD_URL}) \n ${output}"
                    slackSend (color: '#FF0000', message: summary)
                }
            }
        }
    }

    // 'master' branch is published to GH pages
    if (isMaster) {
        build(job: 'Conan-Docs-Publish', propagate: true, wait: true, parameters: [
            [$class: 'StringParameterValue', name: 'latest', value: masterBranch],
            [$class: 'StringParameterValue', name: 'prefix', value: 'https://docs.conan.io/'],
            [$class: 'BooleanParameterValue', name: 'publish', value: true]
        ])
    }
}
