#!groovyâ€‹

node('Linux') {
    String alphaReleaseBranch = 'release/2.0.0-alpha'
    boolean publishDocs = env.BRANCH_NAME == alphaReleaseBranch
    def targetBranch = env.CHANGE_BRANCH
    
    // We also have to clone conan sources for the sphinx autodoc extension
    def conanBranch = (targetBranch == alphaReleaseBranch) ? 'release/2.0-alpha' : 'develop2'
    def conan_repo_url = 'https://github.com/conan-io/conan.git'
    echo "Docs target branch: ${targetBranch}"
    echo "Conan target branch: ${conanBranch}"

    stage('Clone sources') {
        checkout scm
        def cloneConan = "git clone --single-branch -b ${conanBranch} --depth 1 ${conan_repo_url} conan_sources"
        sh(script: cloneConan)
    }

    def image = null
    stage('Build docker image') {
        image = docker.build('conan-docs', '-f .ci/Dockerfile .')  // It should cache the image
    }

    stage('Test build') {
        parallel html: {
            image.inside {
                sh 'make html'
            }
        },
        pdf: {
            image.inside {
                sh 'make latex'
            }
        },
        spelling: {
            image.inside {
                sh 'make spelling'
            }
        },
        linkcheck: {
            image.inside {
                catchError(buildResult: 'SUCCESS', stageResult: 'UNSTABLE', message: 'Linkcheck failed! Please check links and fix broken ones.') {
                    sh(script: 'make linkcheck')
                }
            }
        }
    }

    // For alpha releases we trigger the publish job on the master branch
    // this will republish all the docs including the updated release/2.0.0-alpha
    // latest parameter still has to be 'master'
    if (publishDocs) {
        build(job: 'Conan-Docs-Publish', propagate: true, wait: true, parameters: [
            [$class: 'StringParameterValue', name: 'latest', value: 'master'],
            [$class: 'StringParameterValue', name: 'prefix', value: 'https://docs.conan.io/'],
            [$class: 'BooleanParameterValue', name: 'publish', value: true]
        ])
    }
}
