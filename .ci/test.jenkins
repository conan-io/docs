#!groovyâ€‹

node('Linux') {

    boolean publishDocs = env.BRANCH_NAME.startsWith("release/2.")
    def targetBranch = env.CHANGE_TARGET ? env.CHANGE_TARGET : env.BRANCH_NAME
    
    // We also have to clone conan sources for the sphinx autodoc extension
    def conanBranch = targetBranch.startsWith("release/2.") ? targetBranch : 'develop2'
    def conan_repo_url = 'https://github.com/conan-io/conan.git'

    echo "Docs target branch: ${targetBranch}"
    echo "Conan target branch: ${conanBranch}"
    echo "env.BRANCH_NAME: ${env.BRANCH_NAME}"
    echo "env.CHANGE_BRANCH: ${env.CHANGE_BRANCH}"
    echo "env.CHANGE_TARGET: ${env.CHANGE_TARGET}"

    stage('Clone sources') {
        checkout scm
        def cloneConan = "git clone --single-branch -b ${conanBranch} --depth 1 ${conan_repo_url} conan_sources"
        sh(script: cloneConan)
    }

    def image = null
    stage('Build docker image') {
        image = docker.build('conan-docs', '-f .ci/Dockerfile .')  // It should cache the image
    }

    stage('Test build') {
        parallel html: {
            image.inside {
                // for some reason even adding this to autodoc_mock_imports
                // does not work, se we have to install the real dependency
                sh(script: 'pip3 install colorama')
                sh(script: 'make html')
            }
        },
        pdf: {
            image.inside {
                sh(script: 'make latex')
            }
        },
        spelling: {
            image.inside {
                sh(script: 'pip3 install colorama')
                sh(script: 'make spelling')
            }
        },
        linkcheck: {
            image.inside {
                catchError(buildResult: 'SUCCESS', stageResult: 'UNSTABLE', message: 'Linkcheck failed! Please check links and fix broken ones.') {
                    sh(script: 'make linkcheck')
                }
            }
        }
    }

    // For beta releases we trigger the publish job on the master branch
    // this will republish all the docs including the updated release/2.0.0-beta
    // latest parameter still has to be 'master'
    if (publishDocs) {
        build(job: 'Conan-Docs-Publish', propagate: true, wait: true, parameters: [
            [$class: 'StringParameterValue', name: 'prefix', value: 'https://docs.conan.io/'],
            [$class: 'BooleanParameterValue', name: 'publish', value: true]
        ])
    }
}
